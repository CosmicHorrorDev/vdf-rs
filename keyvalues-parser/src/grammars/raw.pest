WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{"//" ~ (!"\n" ~ ANY)* }

vdf = _{ SOI ~ base_macro* ~ pair ~ "\u{00}"? ~ EOI }

base_macro = { "#base" ~ (quoted_raw_string | unquoted_string ) }

quoted_raw_string = ${ "\"" ~ quoted_inner ~ "\"" }

pairs = _{ pair* }

pair = { key ~ value }

key = _{ quoted_string | unquoted_string }

value = _{ quoted_string | obj | unquoted_string }

obj = { "{" ~ pairs ~ "}" }

quoted_string = ${ "\"" ~ quoted_inner ~ "\"" }
quoted_inner = @{ (!"\"" ~ ANY)* }

unquoted_string = @{ unquoted_char+ }
// The wiki page just states that an unquoted string ends with ", {, }, or any
// whitespace which I feel is likely missing several cases, but for now I will
// follow that information
unquoted_char = {
    !("\"" | "{" | "}" | WHITESPACE)  ~ ANY
}
