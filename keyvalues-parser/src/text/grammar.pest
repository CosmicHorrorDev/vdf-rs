WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{"//" ~ (!"\n" ~ ANY)* }

vdf = _{ SOI ~ pair ~ EOI }

pairs = { pair* }

pair = { key ~ value }

key = _{ quoted_string | unquoted_string }

value = _{ obj | quoted_string | unquoted_string }

obj = { "{" ~ pairs ~ "}" }

quoted_string = ${ "\"" ~ quoted_inner ~ "\"" }
quoted_inner = @{ quoted_char* }
quoted_char = {
    !("\"" | "\\")  ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "r" | "t")
}

unquoted_string = @{ unquoted_char+ }
// The wiki page just states that an unquoted string ends with ", {, }, or any
// whitespace which I feel is likely missing several cases, but for now I will
// follow that information
unquoted_char = {
    !("\"" | "{" | "}" | WHITESPACE)  ~ ANY
}
